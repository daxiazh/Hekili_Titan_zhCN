## Warlock: Affliction - 痛苦术士优化版
## 泰坦时空特别版 - 优化手法
## 核心循环: 痛苦 > 不稳定痛苦 > 鬼影缠身 > 痛苦诅咒 > 暗影箭

## 战前准备
actions.precombat+=/fel_armor,if=buff.armor.down|buff.armor.remains<180
actions.precombat+=/summon_felhunter,if=!pet.active&combat=0
actions.precombat+=/summon_voidwalker,if=!pet.active&combat=0
actions.precombat+=/summon_imp,if=!pet.active&combat=0
actions.precombat+=/life_tap,if=glyph.life_tap.enabled&buff.life_tap.down
actions.precombat+=/potion
actions.precombat+=/shadow_bolt,if=talent.improved_shadow_bolt.enabled&settings.shadow_mastery&debuff.shadow_mastery.down

## 默认动作
## 治疗药水 - 低血量时使用
actions+=/best_healing_potion,if=health.pct<35
actions+=/run_action_list,name=st,if=active_enemies=1&target.health.pct>26
actions+=/run_action_list,name=aoe,if=active_enemies<4&active_enemies>1&target.health.pct>26
actions+=/run_action_list,name=seed,if=active_enemies>3&target.health.pct>26
actions+=/run_action_list,name=execute,if=target.health.pct<26
actions+=/run_action_list,name=life,if=health.pct<25

## 单目标循环
## 痛苦 - 最高优先级DOT
actions.st+=/corruption,if=!ticking|(persistent_multiplier>dot.corruption.pmultiplier&dot.corruption.refreshable)
actions.st+=/hyperspeed_acceleration,if=time_to_die>90
## 不稳定痛苦 - 鬼影存在时施放
actions.st+=/unstable_affliction,if=dot.haunt.remains>cast_time+travel_time+latency&dot.unstable_affliction.remains<cast_time+latency
## 鬼影缠身 - 维护
actions.st+=/haunt,if=!ticking|dot.haunt.remains<cast_time+travel_time+latency
## 不稳定痛苦
actions.st+=/unstable_affliction,if=!ticking&target.time_to_die>15&dot.unstable_affliction.remains<cast_time+latency
## 痛苦诅咒
actions.st+=/curse_of_agony,if=dot.haunt.remains&dot.unstable_affliction.remains&!ticking
actions.st+=/inferno,if=target.time_to_die<=60&mana.pct>20&inferno_enabled
actions.st+=/curse_of_agony,if=debuff.my_curse.down
## 生命分流
actions.st+=/life_tap,if=mana.pct<10|glyph.life_tap.enabled&buff.life_tap.remains<5
actions.st+=/potion,target.health.pct=<26
actions.st+=/shadowflame,if=target.distance<10
## 暗影箭 - 维护暗影精通
actions.st+=/shadow_bolt,if=talent.improved_shadow_bolt.enabled&settings.shadow_mastery&debuff.shadow_mastery.remains<cast_time+2
## 暗影箭填充
actions.st+=/shadow_bolt

## 斩杀阶段 - 灵魂吸取
actions.execute+=/potion
actions.execute+=/use_items
actions.execute+=/unstable_affliction,if=dot.unstable_affliction.remains<cast_time&dot.haunt.remains>cast_time+travel_time+latency&time_to_die>=15
actions.execute+=/haunt,if=!ticking|dot.haunt.remains<=cast_time+travel_time+latency
actions.execute+=/corruption,if=!ticking|(persistent_multiplier>dot.corruption.pmultiplier&dot.corruption.refreshable)
actions.execute+=/curse_of_agony,if=debuff.my_curse.down&time_to_die>=24
actions.execute+=/life_tap,if=mana.pct<10|glyph.life_tap.enabled&buff.life_tap.remains<5
actions.execute+=/hyperspeed_acceleration,if=time_to_die<14
## 灵魂吸取 - 斩杀核心
actions.execute+=/drain_soul,interrupt_if=ticks>=1&(dot.haunt.remains<=cast_time+travel_time+latency|dot.unstable_affliction.remains<=action.unstable_affliction.cast_time+latency&target.time_to_die>=15|debuff.my_curse.down&target.time_to_die>=24)

## 腐蚀之种AOE
actions.seed+=/life_tap,if=mana.pct<30|glyph.life_tap.enabled&buff.life_tap.remains<5
actions.seed+=/shadowflame,if=target.distance<10
actions.seed+=/seed_of_corruption,cycle_targets=1,if=active_enemies>3&!ticking

## 多目标DOT
actions.aoe+=/life_tap,if=mana.pct<30|glyph.life_tap.enabled&buff.life_tap.remains<5
actions.aoe+=/corruption,cycle_targets=1,max_cycle_targets=1,if=!ticking
actions.aoe+=/unstable_affliction,cycle_targets=1,max_cycle_targets=3,if=!ticking
actions.aoe+=/curse_of_agony,cycle_targets=1,max_cycle_targets=3,if=!ticking
actions.aoe+=/haunt,cycle_targets=1,max_cycle_targets=1,if=!ticking
actions.aoe+=/drain_soul,cycle_targets=1,if=target.health.pct<25
actions.aoe+=/shadow_bolt,if=dot.corruption.remains&dot.unstable_affliction.remains&dot.curse_of_agony.remains&dot.haunt.remains&active_enemies<3

## 低血量自救
actions.life+=/death_coil
actions.life+=/drain_life
